import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem


repositories {
    mavenCentral()
}

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'
apply plugin: 'java'
apply plugin: 'jacoco'

apply from: '../config.gradle'

sourceSets {
    main {
        java {
            srcDirs = ["java/src"]
        }
    }
    test {
        java {
            srcDirs = ["java/unit_tests"]
        }
    }
}

task packageNativeFilesInJar(type: Jar) {

    println "Packging native files"
    
    dependsOn jar
    
    destinationDir = project.buildDir
    classifier = "native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'snobotSimJniStatic') {
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                }
            }
        }
    }
}

task combineNativeLibrariesIntoOne(type: Jar) {

    dependsOn packageNativeFilesInJar

    def os_name = org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    classifier = "uber_native-" + os_name;
    
    packageNativeFilesInJar.outputs.files.each { 
        from(zipTree(it))
    }

    from(zipTree('../snobot_sim/build/snobotSim-native-' + os_name + ".jar"))
    from(zipTree('../temp_libs/wpilibj-jni-ABCD-all.jar'))
    
    def wpiutil_path = "../build/dependencies/wpiutil-cpp/" + os_name + "x86-64/" + os_name + "/x86-64/shared"

    from(wpiutil_path) {
        into os_name + "/x86-64/"
        include "*.dll"
        include "*.so"
    }
    
    def hal_path = new File('temp_libs/hal/' + os_name + '/x86-64').getAbsolutePath()
    from(hal_path) {
        into os_name + "/x86-64/"
        include "*.dll"
        include "*.so"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

dependencies {
    compile project(":snobot_sim_utilities")
    
    testCompile 'junit:junit:4.12'
    testCompile 'edu.wpi.first.wpilibj:wpilibj-java:' + getWpilibjVersion()
    compile 'edu.wpi.first.ntcore:ntcore-java:' + getNtCoreVersion()
    testRuntime 'edu.wpi.first.ntcore:ntcore-jni:' + getNtCoreVersion() + ':all'
    //testRuntime jar.outputs.files
    //testRuntime packageNativeFilesInJar.outputs.files
    testRuntime combineNativeLibrariesIntoOne.outputs.files
}

def jniClasses = [
    'com.snobot.simulator.jni.JoystickJni',
    //'com.snobot.simulator.jni.MotorConfigFactoryJni',
    'com.snobot.simulator.jni.RobotStateSingletonJni',
    //'com.snobot.simulator.jni.SimulationConnectorJni',
    'com.snobot.simulator.jni.SnobotSimulatorJni',
    'com.snobot.simulator.jni.module_wrapper.AccelerometerWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.AnalogSourceWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.DigitalSourceWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.EncoderWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.GyroWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.RelayWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.SolenoidWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.SpeedControllerWrapperJni'
]
build.dependsOn packageNativeFilesInJar


model {
    jniConfigs {
        snobotSimJniShared(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
            sourceSets = [ project.sourceSets.main ]
        }
        snobotSimJniStatic(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
    }
    exportsConfigs {
        snobotSimJniShared(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
        snobotSimJniStatic(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
    }
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = '+'
            sharedConfigs = [ snobotSimJniShared: [] ]
            staticConfigs = [ snobotSimJniStatic: [] ]
        }
    }
    components {
        snobotSimJniStatic(NativeLibrarySpec) {
            baseName = 'snobotSimJni'
            sources {
                cpp {
                    source {
                        srcDirs = ['cpp']
                        includes = ['**/*.cpp']
                    }
                    exportedHeaders {
                    }
                }
            }
        }
        snobotSimJniShared(NativeLibrarySpec) {
            baseName = 'snobotSimJni'
            sources {
                cpp {
                    source {
                        srcDirs = ['cpp']
                        includes = ['**/*.cpp']
                    }
                    exportedHeaders {
                    }
                }
            }
        }

    }
    binaries {
        withType(NativeBinarySpec) {
        
            if(it.targetPlatform.architecture.name != "athena")
            {
                project(':snobot_sim').addSnobotSimToLinker(it)
                project(':snobot_sim').addHalSimToLinker(linker)
            }
            else
            {
                buildable = false
            }
        }
        withType(StaticLibraryBinarySpec) {
            it.buildable = false
        }
    }
}

apply from: "publish.gradle"