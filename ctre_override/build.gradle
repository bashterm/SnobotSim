import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem

repositories {
    mavenCentral()
    
    maven {
        url "http://dev.imjac.in/maven/"
    }
    maven {
        url "http://raw.githubusercontent.com/pjreiniger/maven_repo/master/"
    }
}

apply plugin: 'cpp'
apply plugin: 'java'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'

apply from: '../config.gradle'


sourceSets {
    main {
        java {
            srcDirs = ["ctre_source/java/src"]
        }
    }
}
test {
	ignoreFailures = !OperatingSystem.current().isWindows()
}

task packageNativeFilesInJar(type: Jar) {
    destinationDir = project.buildDir
    classifier = "native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'CTRE_PhoenixCCI' || 
                    binary.component.name == 'SnobotSimCppCtre' ||
                    binary.component.name == 'SnobotSimJavaCtre') 
                {
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                    dependsOn binary.buildTask
                }
            }
        }
    }

}

apply from: "$rootDir/captureWpiLibraries.gradle"
task combineNativeLibrariesIntoOne(type: Jar) {

    dependsOn packageNativeFilesInJar
    dependsOn captureWpiLibraries

    def os_name = org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    classifier = "uber_native-" + os_name;
    
    packageNativeFilesInJar.outputs.files.each { 
        from(zipTree(it))
    }
    
    captureWpiLibraries.outputs.files.each {
        from(zipTree(it))
    }
}

build.dependsOn packageNativeFilesInJar

dependencies {
    compile project(":snobot_sim_utilities")
}


def jniClasses = ['com.ctre.phoenix.CTRLoggerJNI',
                  'com.ctre.phoenix.motorcontrol.can.MotControllerJNI',
                  'com.ctre.phoenix.sensors.PigeonImuJNI']

model {
    jniConfigs {
        CtrePhoenixJava(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/../snobot_sim_gui/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
    }
    exportsConfigs {
        CtreMockHooks(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
        CtrePhoenixJava(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
    }

    components {
    
        CtreMockHooks(NativeLibrarySpec) {

            sources {
                cpp {
                    source {
                        srcDirs = [ "mock_hooks/src"]
                        includes = ["**/*.cpp"]
                    }
                    exportedHeaders {
                        srcDirs = [ "ctre_libraries/include",
                                    "mock_hooks/include"]
                    }
                }
            }
        }
        
        CtrePhoenixJava(NativeLibrarySpec) {
            baseName = 'CTRE_PhoenixCCI'
            
            binaries {
                all {
                   it.lib project: ':ctre_override', library: 'CtreMockHooks', linkage: 'shared'
                }
            }
        
            sources {
                cpp {
                    source {
                        srcDirs = ['ctre_java_libraries/src']
                        includes = ["**/*.cpp"]
                    }
                    exportedHeaders {
                        srcDirs = [ "ctre_libraries/include"]
                    }
                }
            }
        }
            
    }
}


apply from: 'publish.gradle'
