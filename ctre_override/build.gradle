import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem

repositories {
    mavenCentral()
    
    maven {
        url "http://dev.imjac.in/maven/"
    }
    maven {
        url "http://raw.githubusercontent.com/pjreiniger/maven_repo/master/"
    }
}

apply plugin: 'cpp'
apply plugin: 'java'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'

apply from: '../config.gradle'


sourceSets {
    main {
        java {
            srcDirs = ["ctre_source/java/src"]
        }
    }
}
test {
	ignoreFailures = !OperatingSystem.current().isWindows()
}

task packageNativeFilesInJar(type: Jar) {
    destinationDir = project.buildDir
    classifier = "native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'CTRLibDriverShared' || 
                    binary.component.name == 'SnobotSimCppCtre' ||
                    binary.component.name == 'SnobotSimJavaCtre') 
                {
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                    dependsOn binary.buildTask
                }
            }
        }
    }

}

apply from: "$rootDir/captureWpiLibraries.gradle"
task combineNativeLibrariesIntoOne(type: Jar) {

    dependsOn packageNativeFilesInJar
    dependsOn captureWpiLibraries

    def os_name = org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    classifier = "uber_native-" + os_name;
    
    packageNativeFilesInJar.outputs.files.each { 
        from(zipTree(it))
    }
    
    captureWpiLibraries.outputs.files.each {
        from(zipTree(it))
    }
}

build.dependsOn packageNativeFilesInJar

dependencies {
    compile project(":snobot_sim_utilities")
}


def jniClasses = ['com.ctre.CanTalonJNI']

model {
    jniConfigs {
        CTRLibDriverShared(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/../snobot_sim_gui/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
    }
    exportsConfigs {
        CTRLibDriverShared(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
    }
    
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getWpiUtilVersion()
            sharedConfigs = [ SnobotSimCppCtre: [],
                              SnobotSimJavaCtre: [],
                              CTRLibDriver: [] ]
        }
        ntcore(DependencyConfig) {
            groupId = 'edu.wpi.first.ntcore'
            artifactId = 'ntcore-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getNtCoreVersion()
            sharedConfigs = [ SnobotSimCppCtre: [],
                              CTRLibDriver: [] ]
        }
        halsim(DependencyConfig) {
            groupId = 'edu.wpi.first.hal'
            artifactId = 'hal'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getHalHeadersVersion()
            sharedConfigs = [ SnobotSimCppCtre: [],
                              CTRLibDriverShared: [],
                              SnobotSimJavaCtre: [] ]
        }
    }

    components {
        SnobotSimJavaCtre(NativeLibrarySpec) {
            baseName = 'SnobotSimJavaCtre'
        
            sources {
                cpp {
                    source {
                        srcDirs = ['ctre_source/cpp/shared/lib',
                                   'snobot_sim/shared/lib', 
                                    'snobot_sim/java_simulator/lib' ]
                        includes = ["**/*.cpp"]
                    }
                    exportedHeaders {
                        srcDirs = [
                                   "ctre_source/cpp/shared/include",
                                   "snobot_sim/java_simulator/include"]
                    }
                }
            }
        }
            
        CTRLibDriverShared(NativeLibrarySpec) {
            baseName = 'CTRLibDriver'
                
            binaries {
                all {
                   it.lib project: ':ctre_override', library: 'SnobotSimJavaCtre', linkage: 'static'
                }
            }
            sources {
                cpp {
                    source {
                        srcDirs = [ 'java/jni', 
                                    "ctre_source/java/jni/cpp"]
                        includes = ["**/*.cpp"]
                    }
                    exportedHeaders {
                        srcDirs = []
                    }
                }
            }
        }
    }
}


apply from: 'publish.gradle'
