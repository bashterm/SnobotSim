import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'
apply plugin: 'google-test-test-suite'

apply from: "${rootDir}/common/config.gradle"


ext {
    staticGtestConfigs = [:]
}

staticGtestConfigs["snobotSimCppTest"] = []
apply from: "${rootDir}/common/gtest.gradle"

ext.addSnobotSimToLinker = { binary->
    binary.lib project: ':snobot_sim', library: 'snobotSimCpp', linkage: 'shared'
}

task packageNativeFilesInJar(type: Jar) {

    destinationDir = project.buildDir
    classifier = "snobotSim-native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'snobotSimCpp') {
                    dependsOn binary.buildTask
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                }
            }
        }
    }
}
build.dependsOn packageNativeFilesInJar


model {
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getWpiUtilVersion()
            sharedConfigs = [ snobotSimCpp: [], snobotSimCppTest: [] ]
        }
        halsim(DependencyConfig) {
            groupId = 'edu.wpi.first.hal'
            artifactId = 'hal-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = allwpilibVersion()
            sharedConfigs = [ snobotSimCpp: [], snobotSimCppTest: [] ]
        }
        adxfamily(DependencyConfig) {
            groupId = 'edu.wpi.first.halsim'
            artifactId = 'halsim_adx_gyro_accelerometer'
            headerClassifier = 'headers'
            ext = 'zip'
            version = allwpilibVersion()
            sharedConfigs = [ snobotSimCpp: [], snobotSimCppTest: [] ]
        }
    }
    components {
        snobotSimCpp(NativeLibrarySpec) {
            baseName = 'snobot_sim'
            
            apply from: "../create_version_file.gradle"
            createCppVersion("src/main/native/cpp/SnobotSim", "SnobotSim", "SnobotSimHalVersion", getVersionName())
            
            sources {
                cpp {
                    source {
                        srcDirs = ['src/main/native/cpp']
                        includes = ['**/*.cpp']
                    }
                    exportedHeaders {
                        srcDirs = ["src/main/native/include"]
                    }
                    lib project: ':sim_extension_navx', library: 'navx_simulator_jni', linkage: 'shared'
                    lib project: ':3rdParty', library: 'yamlcpp', linkage: 'static'
                }
            }
            appendDebugPathToBinaries(binaries)
        }
    }
    testSuites {
        snobotSimCppTest(GoogleTestTestSuiteSpec) {
            for(NativeComponentSpec c : $.components) {
                if (c.name == "snobotSimCpp") {
                    testing c
                    break
                }
            }
            sources {
                cpp {
                    source {
                        srcDirs 'src/test/native/cpp'
                        include '**/*.cpp'
                    }
                }
            }
        }

    }
}

tasks.withType(RunTestExecutable) {
    args "--gtest_output=xml:test_detail.xml"
    outputs.dir outputDir
}

apply from: 'publish.gradle'
