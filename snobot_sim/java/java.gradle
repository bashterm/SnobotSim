import org.gradle.internal.os.OperatingSystem

apply plugin: 'java'
apply plugin: 'net.ltgt.errorprone'

repositories {
    mavenCentral()
}

configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.9'
}

def generatedJNIHeaderLoc = "${buildDir}/include"

sourceSets {
    main {
        java {
            srcDirs = ["${rootDir}/snobot_sim/java/src"]
        }
    }
}

jar {

    baseName = 'snobotSim'
    duplicatesStrategy = 'exclude'

    dependsOn { classes }
}

project.tasks.whenTaskAdded { task ->

    if (task.name == 'ntcoreX64SharedLibrary' || task.name == 'ntcoreX86SharedLibrary')
        jar.dependsOn task
}

task networktablesJavaSource(type: Jar, dependsOn: classes) {
    description = 'Generates the source jar for NetworkTables java'
    group = 'WPILib'
    baseName = 'ntcore'
    classifier = "sources"
    duplicatesStrategy = 'exclude'

    from sourceSets.main.allJava
}

task networktablesJavadoc(type: Jar, dependsOn: javadoc) {
    description = 'Generates the javadoc jar for NetworkTables java'
    group = 'WPILib'
    baseName = 'ntcore'
    classifier = "javadoc"
    duplicatesStrategy = 'exclude'

    from javadoc.destinationDir
}

build.dependsOn networktablesJavaSource
build.dependsOn networktablesJavadoc

/**
 * Generates the JNI headers
 */
task jniHeadersNetworkTables {
    description = 'Generates JNI headers from edu.wpi.first.wpilibj.networktables.*'
    group = 'WPILib'
    def outputFolder = file(generatedJNIHeaderLoc)
    inputs.files sourceSets.main.output
    outputs.file outputFolder
    doLast {
        outputFolder.mkdirs()
        exec {
            executable org.gradle.internal.jvm.Jvm.current().getExecutable('javah')
            args '-d', outputFolder
            args '-classpath', sourceSets.main.output.classesDir
            
            
            //////////////////////////////////////////////////////
            args 'com.snobot.simulator.jni.module_wrapper.AccelerometerWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.AnalogSourceWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.DigitalSourceWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.EncoderWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.GyroWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.RelayWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.SolenoidWrapperJni'
            args 'com.snobot.simulator.jni.module_wrapper.SpeedControllerWrapperJni'
            args 'com.snobot.simulator.jni.RobotStateSingletonJni'
            args 'com.snobot.simulator.jni.SimulationConnectorJni'
            args 'com.snobot.simulator.jni.SnobotSimulatorJni'
            args 'com.snobot.simulator.jni.JoystickJni'
            
        }
    }
}

ext.getNativeJNISymbols = {
    def symbolsList = []

    jniHeadersNetworkTables.outputs.files.each {
        FileTree tree = fileTree(dir: it)
        tree.each { File file ->
            file.eachLine { line ->
                if (line.trim()) {
                    if (line.startsWith("JNIEXPORT ") && line.contains('JNICALL')) {
                        def (p1, p2) = line.split('JNICALL').collect { it.trim() }
                        // p2 is our JNI call
                        symbolsList << p2
                    }
                }
            }
        }
    }

    return symbolsList
}

clean {
    delete generatedJNIHeaderLoc
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// This creates a lambda that the main build.gradle can access, which sets up the JNI includes for the
// target build platform. This lambda is exposed as a property in the main build.gradle.
ext.setupJniIncludes = { binaries ->
    def platformSpecificIncludeFlag = { loc, cppCompiler ->
        if (OperatingSystem.current().isWindows()) {
            cppCompiler.args "/I$loc"
        } else {
            cppCompiler.args '-I', loc
        }
    }
    binaries.all {
        tasks.withType(CppCompile) {
            if (buildPlatform == 'arm') {
                cppCompiler.args '-I', file("${rootDir}/java/arm-linux").absolutePath
                cppCompiler.args '-I', file("${rootDir}/java/arm-linux/linux").absolutePath
            } else {
                def jdkLocation = org.gradle.internal.jvm.Jvm.current().javaHome
                platformSpecificIncludeFlag("${jdkLocation}/include", cppCompiler)

                if (targetPlatform.operatingSystem.macOsX) {
                    platformSpecificIncludeFlag("${jdkLocation}/include/darwin", cppCompiler)
                } else if (targetPlatform.operatingSystem.linux) {
                    platformSpecificIncludeFlag("${jdkLocation}/include/linux", cppCompiler)
                } else if (targetPlatform.operatingSystem.windows) {
                    platformSpecificIncludeFlag("${jdkLocation}/include/win32", cppCompiler)
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    platformSpecificIncludeFlag("${jdkLocation}/include/freebsd", cppCompiler)
                } else if (file("$jdkLocation/include/darwin").exists()) {
                    // TODO: As of Gradle 2.8, targetPlatform.operatingSystem.macOsX returns false
                    // on El Capitan. We therefore manually test for the darwin folder and include it
                    // if it exists
                    platformSpecificIncludeFlag("${jdkLocation}/include/darwin", cppCompiler)
                }
            }

            jniHeadersNetworkTables.outputs.files.each { file ->
                if (buildPlatform == 'arm') {
                    cppCompiler.args '-I', file.getPath()
                } else {
                    platformSpecificIncludeFlag(file.getPath(), cppCompiler)
                }
            }

            dependsOn jniHeadersNetworkTables
        }
    }
}
