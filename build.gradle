import org.gradle.internal.os.OperatingSystem

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
        classpath 'gradle.plugin.edu.wpi.first.wpilib.versioning:wpilib-version-plugin:1.4'
    }
}

task download_wpilibs (type: Copy) {

    def output_folder = new File("${rootDir}/external_dependencies/")

    if( !output_folder.exists() ) {
      output_folder.mkdirs()
    }
    
    
    if(project.hasProperty('local_wpilib'))
    {
        from project.local_wpilib
        into output_folder
    }
    else
    {
        repositories {
            maven {
                url "http://raw.githubusercontent.com/pjreiniger/maven_repo/master/"
            }
        }
        
        configurations.create("snobotSimDeps")
        
        dependencies {
            if (OperatingSystem.current().isWindows()) {
                snobotSimDeps 'edu.wpilib:wpilibSim:1.0'
            } else if (OperatingSystem.current().isLinux()) {
                snobotSimDeps 'edu.wpilib:wpilibSim:1.0:linux@zip'
            }
        }
        
        configurations.snobotSimDeps.files.each { file ->
            from zipTree(file)
            into output_folder
        }
    }
}

ext.getPlatformPath = { binary ->
    if (binary.targetPlatform.architecture.arm) {
        return 'Linux/arm'
    } else if (binary.targetPlatform.operatingSystem.linux) {
        if (binary.targetPlatform.architecture.amd64) {
            return 'Linux/amd64'
        } else {
            return 'Linux/' + binary.targetPlatform.architecture.name
        }
    } else if (binary.targetPlatform.operatingSystem.windows) {
        if (binary.targetPlatform.architecture.amd64) {
            return 'Windows/amd64'
        } else {
            return 'Windows/' + binary.targetPlatform.architecture.name
        }
    } else if (binary.targetPlatform.operatingSystem.macOsX) {
        if (binary.targetPlatform.architecture.amd64) {
            return 'Mac OS X/x86_64'
        } else {
            return 'Mac OS X/' + binary.targetPlatform.architecture.name
        }
    } else {
        return binary.targetPlatform.operatingSystem.name + '/' + binary.targetPlatform.architecture.name
    }
}

ext.setupDefines = { project, binaries ->
    binaries.all {
        if (project.hasProperty('debug')) {
            project.setupDebugDefines(cppCompiler, linker)
        } else {
            project.setupReleaseDefines(cppCompiler, linker)
        }
    }
}

apply from: "snobot_sim/snobot_sim.gradle"
apply from: "wpiutil.gradle"
apply from: "hal_override/hal_override.gradle"
apply from: "ctre_override/ctre_override.gradle"
apply from: "snobot_sim_gui/snobot_sim_gui.gradle"

// Empty task for build so that ntcoreSourceZip and wpiutilSourceZip will be
// built when running ./gradlew build
task build

build.dependsOn download_wpilibs
//apply from: "unit_tests/unit_tests.gradle"


task clean(type: Delete) {
    delete "build"
    delete "native"
    delete "arm"
    delete "external_dependencies"
    delete fileTree(".") { include "**/snobotSim*.jar" }
    delete fileTree(".") { include "**/snobotSim*.zip" }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

