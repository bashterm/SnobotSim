
apply plugin: 'maven-publish'

task cppSourcesZip(type: Zip) {
    destinationDir = project.buildDir
    
    classifier = "sources"
    
    from('src/main/native') {
        into 'SnobotSim'
    }
}


task javadoc(type: Javadoc, overwrite: true) {
    source = sourceSets.main.allJava
    
    javadoc.options.links("http://docs.oracle.com/javase/8/docs/api/")
    options.addStringOption "tag", "pre:a:Pre-Condition"
//    options.addStringOption('Xdoclint:accessibility,syntax,reference,html', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task copyDependencies(type: Zip)  {
        
    doFirst {
        copy {
            configurations.native3rdPartyDeps.each {
                println it
                from zipTree(it)
                into destinationDir.toString() + "/jinput_native"
                include "*.dll"
                include "*.pdb"
                include "*.so"
            }
        }
    }

    destinationDir = project.buildDir
    classifier = "javadeps"
    
    from configurations.compile {
        into "dependencies"
        exclude "jinput-platform-2.0.7-natives-*.jar"
    }
    
    from (destinationDir.toString() + "/jinput_native") {
        into "native"
    }
    
    
}

publishing {
    repositories {
       maven {
           url maven_publishing_path
       }
    }
    publications {
        snobot_sim_gui(MavenPublication) {
            groupId 'com.snobot.simulator'
            artifactId 'snobot_sim_gui'
            version maven_version
            
            artifact jar
            artifact cppSourcesZip
            artifact javadocJar
            artifact copyDependencies
            project(':snobot_sim_gui').configurations.snobotSimGuiNativeDeps.artifacts.each { artifact it }
        }
        
        snobot_sim_gui_application(MavenPublication) {
            groupId 'com.snobot.simulator'
            artifactId 'snobot_sim_gui_application'
            version maven_version
            
            artifact distZip
            artifact distTar
        }
    }

}